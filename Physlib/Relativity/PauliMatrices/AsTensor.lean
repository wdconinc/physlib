/-
Copyright (c) 2024 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
module

public import Physlib.Relativity.Tensors.ComplexTensor.Weyl.Two
public import Physlib.Relativity.Tensors.ComplexTensor.Vector.Pre.Basic
/-!

## Pauli matrices as a tensor

The results in this file are primarily used to show that
the pauli matrices in invariant under the `SL(2,ℂ)` action.

-/

@[expose] public section

namespace PauliMatrix

open Complex
open Lorentz
open Fermion
open TensorProduct

noncomputable section

open Matrix
open MatrixGroups
open Complex
open TensorProduct

/-- The tensor `σ^μ^a^{dot a}` based on the Pauli-matrices as an element of
  `complexContr ⊗ leftHanded ⊗ rightHanded`. -/
def asTensor : (ContrℂModule ⊗[ℂ] (LeftHandedWeyl ⊗[ℂ] RightHandedWeyl)) :=
  ∑ i, complexContrBasis i ⊗ₜ leftRightToMatrix.symm (pauliBasis i)

/-- The expansion of `asTensor` into complexContrBasis basis vectors . -/
lemma asTensor_expand_complexContrBasis : asTensor =
    complexContrBasis (Sum.inl 0) ⊗ₜ leftRightToMatrix.symm (pauliBasis (Sum.inl 0))
    + complexContrBasis (Sum.inr 0) ⊗ₜ leftRightToMatrix.symm (pauliBasis (Sum.inr 0))
    + complexContrBasis (Sum.inr 1) ⊗ₜ leftRightToMatrix.symm (pauliBasis (Sum.inr 1))
    + complexContrBasis (Sum.inr 2) ⊗ₜ leftRightToMatrix.symm (pauliBasis (Sum.inr 2)) := by
  rfl

/-- The expansion of the pauli matrix `σ₀` in terms of a basis of tensor product vectors. -/
lemma leftRightToMatrix_σSA_inl_0_expand : leftRightToMatrix.symm (pauliBasis (Sum.inl 0)) =
    leftBasis 0 ⊗ₜ rightBasis 0 + leftBasis 1 ⊗ₜ rightBasis 1 := by
  rw [leftRightToMatrix_symm_expand_tmul]
  simp [pauliBasis, pauliSelfAdjoint, pauliMatrix]

/-- The expansion of the pauli matrix `σ₁` in terms of a basis of tensor product vectors. -/
lemma leftRightToMatrix_σSA_inr_0_expand : leftRightToMatrix.symm (pauliBasis (Sum.inr 0)) =
    leftBasis 0 ⊗ₜ rightBasis 1 + leftBasis 1 ⊗ₜ rightBasis 0:= by
  rw [leftRightToMatrix_symm_expand_tmul]
  simp [pauliBasis, pauliSelfAdjoint, pauliMatrix]

/-- The expansion of the pauli matrix `σ₂` in terms of a basis of tensor product vectors. -/
lemma leftRightToMatrix_σSA_inr_1_expand : leftRightToMatrix.symm (pauliBasis (Sum.inr 1)) =
    -(I • leftBasis 0 ⊗ₜ[ℂ] rightBasis 1) + I • leftBasis 1 ⊗ₜ[ℂ] rightBasis 0 := by
  simp [leftRightToMatrix_symm_expand_tmul, pauliBasis, pauliSelfAdjoint, pauliMatrix]
  module

set_option backward.isDefEq.respectTransparency false in
/-- The expansion of the pauli matrix `σ₃` in terms of a basis of tensor product vectors. -/
lemma leftRightToMatrix_σSA_inr_2_expand : leftRightToMatrix.symm (pauliBasis (Sum.inr 2)) =
    leftBasis 0 ⊗ₜ rightBasis 0 - leftBasis 1 ⊗ₜ rightBasis 1 := by
  simp [leftRightToMatrix_symm_expand_tmul, pauliBasis, pauliSelfAdjoint, pauliMatrix]
  module

/-- The expansion of `asTensor` into complexContrBasis basis of tensor product vectors. -/
lemma asTensor_expand : asTensor =
    complexContrBasis (Sum.inl 0) ⊗ₜ (leftBasis 0 ⊗ₜ rightBasis 0)
    + complexContrBasis (Sum.inl 0) ⊗ₜ (leftBasis 1 ⊗ₜ rightBasis 1)
    + complexContrBasis (Sum.inr 0) ⊗ₜ (leftBasis 0 ⊗ₜ rightBasis 1)
    + complexContrBasis (Sum.inr 0) ⊗ₜ (leftBasis 1 ⊗ₜ rightBasis 0)
    - I • complexContrBasis (Sum.inr 1) ⊗ₜ (leftBasis 0 ⊗ₜ rightBasis 1)
    + I • complexContrBasis (Sum.inr 1) ⊗ₜ (leftBasis 1 ⊗ₜ rightBasis 0)
    + complexContrBasis (Sum.inr 2) ⊗ₜ (leftBasis 0 ⊗ₜ rightBasis 0)
    - complexContrBasis (Sum.inr 2) ⊗ₜ (leftBasis 1 ⊗ₜ rightBasis 1) := by
  rw [asTensor_expand_complexContrBasis]
  rw [leftRightToMatrix_σSA_inl_0_expand, leftRightToMatrix_σSA_inr_0_expand,
    leftRightToMatrix_σSA_inr_1_expand, leftRightToMatrix_σSA_inr_2_expand]
  simp only [Fin.isValue, tmul_add, tmul_neg, tmul_smul, tmul_sub]
  rfl

set_option backward.isDefEq.respectTransparency false in
/-- The tensor `σ^μ^a^{dot a}` based on the Pauli-matrices as a morphism,
  `𝟙_ (Rep ℂ SL(2,ℂ)) ⟶ complexContr ⊗ leftHanded ⊗ rightHanded` manifesting
  the invariance under the `SL(2,ℂ)` action. -/
def asConsTensor :
    (Representation.trivial ℂ SL(2,ℂ) ℂ).IntertwiningMap
    (ContrℂModule.SL2CRep.tprod (leftHandedRep.tprod rightHandedRep)) where

    toFun := fun a =>
      let a' : ℂ := a
      a' • asTensor
    map_add' := fun x y => by
      simp only [add_smul]
    map_smul' := fun m x => by
      simp only [smul_smul]
      rfl
    isIntertwining' M := by
      refine LinearMap.ext fun x : ℂ => ?_
      change x • asTensor =
        (TensorProduct.map (ContrℂModule.SL2CRep M)
          (TensorProduct.map (leftHandedRep M) (rightHandedRep M))) (x • asTensor)
      simp only [map_smul]
      apply congrArg
      nth_rewrite 2 [asTensor]
      simp only [map_sum, map_tmul]
      symm
      calc _ = ∑ x, ((ContrℂModule.SL2CRep M) (complexContrBasis x) ⊗ₜ[ℂ]
        leftRightToMatrix.symm (SL2C.toSelfAdjointMap M (pauliBasis x))) := by
            refine Finset.sum_congr rfl (fun x _ => ?_)
            rw [← leftRightToMatrix_ρ_symm_selfAdjoint]
        _ = ∑ x, ((∑ i, (SL2C.toLorentzGroup M).1 i x • (complexContrBasis i)) ⊗ₜ[ℂ]
            ∑ j, leftRightToMatrix.symm ((SL2C.toLorentzGroup M⁻¹).1 x j • (pauliBasis j))) := by
            refine Finset.sum_congr rfl (fun x _ => ?_)
            rw [SL2CRep_ρ_basis, SL2C.toSelfAdjointMap_pauliBasis]
            simp only [SL2C.toLorentzGroup_apply_coe, Fintype.sum_sum_type, Finset.univ_unique,
              Fin.default_eq_zero, Fin.isValue, Finset.sum_singleton, map_inv,
              LorentzGroup.inv_eq_dual, AddSubgroup.coe_add, selfAdjoint.val_smul,
              AddSubgroup.val_finsetSum, map_add, map_sum]
        _ = ∑ x, ∑ i, ∑ j, ((SL2C.toLorentzGroup M).1 i x • (complexContrBasis i)) ⊗ₜ[ℂ]
              leftRightToMatrix.symm.toLinearMap
                ((SL2C.toLorentzGroup M⁻¹).1 x j • (pauliBasis j)) := by
            refine Finset.sum_congr rfl (fun x _ => ?_)
            rw [sum_tmul]
            refine Finset.sum_congr rfl (fun i _ => ?_)
            rw [tmul_sum]
            rfl
        _ = ∑ x, ∑ i, ∑ j, ((SL2C.toLorentzGroup M).1 i x * (SL2C.toLorentzGroup M⁻¹).1 x j)
            • ((complexContrBasis i)) ⊗ₜ[ℂ] leftRightToMatrix.symm ((pauliBasis j)) := by
            refine Finset.sum_congr rfl (fun x _ => (Finset.sum_congr rfl (fun i _ =>
              (Finset.sum_congr rfl (fun j _ => ?_)))))
            simp only [SL2C.toLorentzGroup_apply_coe, map_inv, LorentzGroup.inv_eq_dual,
              LinearMap.map_smul_of_tower, LinearEquiv.coe_coe]
            rw [smul_tmul, smul_smul, ← tmul_smul]
        _ = ∑ i, ∑ j, ∑ x, ((SL2C.toLorentzGroup M).1 i x * (SL2C.toLorentzGroup M⁻¹).1 x j)
            • ((complexContrBasis i)) ⊗ₜ[ℂ] leftRightToMatrix.symm ((pauliBasis j)) := by
            rw [Finset.sum_comm]
            exact Finset.sum_congr rfl (fun x _ => Finset.sum_comm)
        _ = ∑ i, ∑ j, ∑ x, (((SL2C.toLorentzGroup M).1 i x *
            (SL2C.toLorentzGroup M⁻¹).1 x j : ℝ) : ℂ)
            • ((complexContrBasis i)) ⊗ₜ[ℂ] leftRightToMatrix.symm ((pauliBasis j)) := rfl
        _ = ∑ i, ∑ j, (∑ x, (SL2C.toLorentzGroup M).1 i x * (SL2C.toLorentzGroup M⁻¹).1 x j : ℂ)
            • ((complexContrBasis i)) ⊗ₜ[ℂ] leftRightToMatrix.symm ((pauliBasis j)) := by
            refine Finset.sum_congr rfl (fun i _ => (Finset.sum_congr rfl (fun j _ => ?_)))
            rw [← Finset.sum_smul]
            simp
        _ = ∑ i, ∑ j, (∑ x, (SL2C.toLorentzGroup M).1 i x * (SL2C.toLorentzGroup M⁻¹).1 x j : ℝ)
            • ((complexContrBasis i)) ⊗ₜ[ℂ] leftRightToMatrix.symm ((pauliBasis j)) := by
            refine Finset.sum_congr rfl (fun i _ => (Finset.sum_congr rfl (fun j _ => ?_)))
            congr
            simp
        _ = ∑ i, ∑ j, ((1 : Matrix (Fin 1 ⊕ Fin 3) (Fin 1 ⊕ Fin 3) ℝ) i j : ℝ)
          • ((complexContrBasis i)) ⊗ₜ[ℂ] leftRightToMatrix.symm ((pauliBasis j)) := by
            refine Finset.sum_congr rfl (fun i _ => (Finset.sum_congr rfl (fun j _ => ?_)))
            congr
            change ((SL2C.toLorentzGroup M) * (SL2C.toLorentzGroup M⁻¹)).1 i j = _
            rw [← SL2C.toLorentzGroup.map_mul]
            simp only [mul_inv_cancel, _root_.map_one, lorentzGroupIsGroup_one_coe]
        _ = asTensor := by
          refine Finset.sum_congr rfl (fun i _ => ?_)
          rw [Finset.sum_eq_single i (fun b _ hb => ?_) (fun hb => ?_)]
          · simp only [one_apply_eq, one_smul]
          · simp [one_apply_ne' hb]
            module
          · simp only [Finset.mem_univ, not_true_eq_false] at hb

/-- The map `𝟙_ (Rep ℂ SL(2,ℂ)) ⟶ complexContr ⊗ leftHanded ⊗ rightHanded` corresponding
  to Pauli matrices, when evaluated on `1` corresponds to the tensor `PauliMatrix.asTensor`. -/
lemma asConsTensor_apply_one : asConsTensor (1 : ℂ) = asTensor := by
  change (1 : ℂ) • asTensor = asTensor
  simp only [one_smul]

end
end PauliMatrix
