/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
module

public import Physlib.Relativity.Tensors.Product
public import Physlib.Relativity.Tensors.Evaluation
public import Mathlib.Topology.Algebra.Module.FiniteDimension
/-!

# Tensorial class

## i. Overview

We define a class called `Tensorial`.
This class is used to enable the use of index notation on a type `M` via a linear equivalence to a
tensor of a `TensorSpecies`.

We define the class `Tensorial` here, and provide an API around its use.

## ii. Key results

- `Tensorial` : The class used to allow index notation on a type `M`.
- `Tensorial.numIndices` : The number of indices of an element of an `M`
  carrying a tensorial instance.
- `Tensorial.mulAction` : The action of the group `G` on a
  type `M` carrying a tensorial instance.
- `Tensorial.prod` : The product of two tensorial instances is a tensorial instance.

## iii. Table of contents

- A. Defining the tensorial class
  - A.1. Tensors carry a tensorial instance
  - A.2. The number of indices
- B. The action of the group on a module with a tensorial instance
  - B.1. Relation between the action and the equivalence to tensors
  - B.2. Linear properties of the action
  - B.3. The action as a linear map
  - B.4. The SMulCommClass property
- C. Properties of the basis
- D. Products of tensorial instances
  - D.1. The equivalence to tensors on products
  - D.2. The group action on products
  - D.3. The basis on products
- E. Continuous properties
  - E.1. Finite dimensionality
  - E.2. The map to tensors as a continuous linear equivalence
  - E.3. The Lorentz action as a continuous linear equivalence

## iv. References

There are no known references for this material.

-/

@[expose] public section

namespace TensorSpecies

variable {k : Type} [CommRing k] {C : Type} {G : Type} [Group G]
    {V : C → Type} [∀ c, AddCommGroup (V c)] [∀ c, Module k (V c)]
    {basisIdx : C → Type} [∀ c, Fintype (basisIdx c)] [∀ c, DecidableEq (basisIdx c)]
    {rep : (c : C) → Representation k G (V c)} {b : (c : C) → Module.Basis (basisIdx c) k (V c)}
    {S : TensorSpecies k C G V basisIdx rep b}
attribute [-simp] LinearEquiv.cast_apply

/-!

## A. Defining the tensorial class

We first define the `Tensorial` class.

-/

/-- The tensorial class is used to define a tensor structure on a type `M` through a
  linear equivalence with a module `S.Tensor c` for `S` a tensor species. -/
class Tensorial {n : outParam ℕ}

    {k : outParam Type} [CommRing k] {C : outParam Type} {G : outParam Type} [Group G]
    {V :outParam (C → Type)} [∀ c, AddCommGroup (V c)] [∀ c, Module k (V c)]
    {basisIdx : outParam (C → Type)} [∀ c, Fintype (basisIdx c)] [∀ c, DecidableEq (basisIdx c)]
    {rep : outParam ((c : C) → Representation k G (V c))}
    {b : outParam ((c : C) → Module.Basis (basisIdx c) k (V c))}
    (S : outParam (TensorSpecies k C G V basisIdx rep b))
    (c :outParam (Fin n → C)) (M : Type)
    [AddCommMonoid M] [Module k M] where
  /-- The equivalence between `M` and `S.Tensor c` in a tensorial instance. -/
  toTensor : M ≃ₗ[k] S.Tensor c

namespace Tensorial

variable {n : ℕ} {c : Fin n → C} {M : Type} [AddCommMonoid M] [Module k M]

/-!

### A.1. Tensors carry a tensorial instance

The module of tensors of a tensor species carries a canonical tensorial instance,
through the equivalence.

-/
noncomputable instance self {n : ℕ} (S : TensorSpecies k C G V basisIdx rep b) (c : Fin n → C) :
    Tensorial S c (S.Tensor c) where
  toTensor := LinearEquiv.refl k (S.Tensor c)

@[simp]
lemma self_toTensor_apply {n : ℕ} (S : TensorSpecies k C G V basisIdx rep b)
    (c : Fin n → C) (t : S.Tensor c) :
    Tensorial.toTensor t = t := by
  rw [Tensorial.toTensor]
  rfl

/-!

### A.2. The number of indices

-/

/-- The number of indices of a elements `t : M` where `M` carries a tensorial instance. -/
noncomputable def numIndices (t : M) [Tensorial S c M] : ℕ :=
  TensorSpecies.numIndices (S := S) (toTensor t)

/-!

## B. The action of the group on a module with a tensorial instance

We now define the action of the group `G` on a type `M` carrying a tensorial instance.

-/

noncomputable instance (priority := high) smulAction [Tensorial S c M] : SMul G M where
  smul g m := toTensor.symm (g • toTensor m)

set_option backward.isDefEq.respectTransparency false in
noncomputable instance mulAction [Tensorial S c M] : MulAction G M where
  one_smul m := by
    change toTensor.symm (1 • toTensor m) = _
    simp
  mul_smul g h m := by
    change _ = toTensor.symm (g • toTensor (toTensor.symm (h • toTensor m)))
    simp only [LinearEquiv.apply_symm_apply]
    rw [← mul_smul]
    rfl

/-!

### B.1. Relation between the action and the equivalence to tensors

-/

lemma smul_eq {g : G} {t : M} [Tensorial S c M] :
    g • t = toTensor.symm (g • toTensor t) := by
  rw [Tensorial.toTensor]
  rfl

lemma toTensor_smul {g : G} {t : M} [Tensorial S c M] :
    toTensor (g • t) = g • toTensor t := by
  rw [smul_eq]
  simp

lemma smul_toTensor_symm {g : G} {t : Tensor S c} [self : Tensorial S c M] :
    g • (toTensor (self := self).symm t) = toTensor.symm (g • t) := by
  rw [smul_eq]
  simp

/-!

### B.2. Linear properties of the action

-/

set_option backward.isDefEq.respectTransparency false in
noncomputable instance (priority := high) distribMulAction [Tensorial S c M] :
    DistribMulAction G M where
  smul_add g m m' := by
    apply toTensor.injective
    simp [toTensor_smul, map_add]
  smul_zero g := by
    apply toTensor.injective
    simp only [toTensor_smul, map_zero, Tensor.actionT_zero]

/-!

### B.3. The action as a linear map

-/

set_option backward.isDefEq.respectTransparency false in
/-- The action of the group on a `Tensorial` instance as a linear map. -/
noncomputable def smulLinearMap (g : G) [Tensorial S c M] : M →ₗ[k] M where
  toFun m := g • m
  map_add' x y := by
    apply toTensor.injective
    simp [toTensor_smul]
  map_smul' c x := by
    apply toTensor.injective
    simp [toTensor_smul]

lemma smulLinearMap_apply {g : G} [Tensorial S c M] (m : M) :
    smulLinearMap g m = g • m := rfl

/-!

### B.4. The SMulCommClass property

-/

set_option backward.isDefEq.respectTransparency false in
instance [Tensorial S c M] : SMulCommClass k G M where
  smul_comm c g m := by
    apply toTensor.injective
    simp [toTensor_smul]

/-!

## C. Properties of the basis

We now prove some properties of the basis induced on a `Tensorial` instance.

-/

lemma basis_toTensor_apply [Tensorial S c M] (m : M) :
    (Tensor.basis c).repr (toTensor m) = ((Tensor.basis c).map toTensor.symm).repr m := rfl

/-!

## D. Products of tensorial instances

-/

open TensorProduct

noncomputable instance (priority := high) prod [Tensorial S c M] {n2 : ℕ} {c2 : Fin n2 → C}
    {M₂ : Type} [AddCommMonoid M₂] [Module k M₂] [Tensorial S c2 M₂] :
    Tensorial S (Fin.append c c2) (M ⊗[k] M₂) where
  toTensor := (TensorProduct.congr toTensor toTensor).trans
    (Tensor.tensorEquivProd)

/-!

### D.1. The equivalence to tensors on products

-/

lemma toTensor_tprod {n2 : ℕ} {c2 : Fin n2 → C} {M₂ : Type}
    [Tensorial S c M] [AddCommMonoid M₂] [Module k M₂]
    [Tensorial S c2 M₂] (m : M) (m2 : M₂) :
    toTensor (m ⊗ₜ[k] m2) = Tensor.prodT (toTensor m) (toTensor m2) := rfl

/-!

### D.2. The group action on products

-/

set_option backward.isDefEq.respectTransparency false in
lemma smul_prod {n2 : ℕ} {c2 : Fin n2 → C} {M₂ : Type}
    [Tensorial S c M] [AddCommMonoid M₂] [Module k M₂]
    [Tensorial S c2 M₂] (g : G) (m : M) (m2 : M₂) :
    g • (m ⊗ₜ[k] m2) = (g • m) ⊗ₜ[k] (g • m2) := by
  apply toTensor.injective
  simp [toTensor_smul]
  rw [toTensor_tprod, toTensor_tprod]
  rw [← Tensor.prodT_equivariant, toTensor_smul, toTensor_smul]

/-!

### D.3. The basis on products

-/

open Tensor in
lemma basis_map_prod {n2 : ℕ} {c2 : Fin n2 → C} {M₂ : Type}
    [Tensorial S c M] [AddCommMonoid M₂] [Module k M₂]
    [Tensorial S c2 M₂] :
    (Tensor.basis (S := S) (Fin.append c c2)).map
      (toTensor (M := (M ⊗[k] M₂))).symm =
    (((Tensor.basis (S := S) c).map (toTensor (M := M)).symm).tensorProduct
    ((Tensor.basis (S := S) c2).map (toTensor (M := M₂)).symm)).reindex
    (ComponentIdx.prod.symm) := by
  rw [Tensor.basis_prod_eq]
  ext b
  simp only [ComponentIdx.prod, Module.Basis.map_apply, Module.Basis.coe_reindex,
    Equiv.symm_symm, Equiv.coe_fn_mk, Function.comp_apply, Module.Basis.tensorProduct_apply]
  apply toTensor.injective
  simp only [LinearEquiv.apply_symm_apply]
  rw [toTensor_tprod]
  simp only [LinearEquiv.apply_symm_apply]
  rfl

open Tensor in
lemma prod_basis_of_map_reindex {n2 : ℕ} {c2 : Fin n2 → C} {M₂ : Type}
    [Tensorial S c M] [AddCommMonoid M₂] [Module k M₂]
    [Tensorial S c2 M₂] {ι ι2 : Type} {b : Module.Basis ι k M} {b2 : Module.Basis ι2 k M₂}
    {e : Tensor.ComponentIdx c ≃ ι} {e2 : Tensor.ComponentIdx c2 ≃ ι2}
    (h : b = ((Tensor.basis (S := S) c).map toTensor.symm).reindex e)
    (h2 : b2 = ((Tensor.basis (S := S) c2).map toTensor.symm).reindex e2) :
    b.tensorProduct b2 = ((Tensor.basis (S := S) (Fin.append c c2)).map
    (toTensor (S := S) (M := M ⊗[k] M₂)).symm).reindex
    (ComponentIdx.prod.trans (e.prodCongr e2)) := by
  ext ⟨i, j⟩
  simp_rw [Tensorial.basis_map_prod, Module.Basis.tensorProduct_apply]
  simp [h, h2]

open Tensor in
lemma prod_tensor_basis_eq_map_reindex {n2 : ℕ} {c2 : Fin n2 → C} {M₂ : Type}
    [Tensorial S c M] [AddCommMonoid M₂] [Module k M₂]
    [Tensorial S c2 M₂] {ι ι2 : Type} {b : Module.Basis ι k M} {b2 : Module.Basis ι2 k M₂}
    {e : Tensor.ComponentIdx c ≃ ι} {e2 : Tensor.ComponentIdx c2 ≃ ι2}
    (h : b = ((Tensor.basis (S := S) c).map toTensor.symm).reindex e)
    (h2 : b2 = ((Tensor.basis (S := S) c2).map toTensor.symm).reindex e2) :
    Tensor.basis (S := S) (Fin.append c c2) =
    ((b.tensorProduct b2).map (toTensor (S := S) (M := M ⊗[k] M₂))).reindex
    (ComponentIdx.prod.trans (e.prodCongr e2)).symm := by
  rw [Tensor.basis_prod_eq]
  ext r
  simp
  obtain ⟨⟨i, j⟩, rfl⟩ := ComponentIdx.prod.symm.surjective r
  simp [h, h2, tensorEquivProd, toTensor_tprod]

attribute [-simp] Matrix.cons_val_zero Matrix.cons_val Fin.succAbove_zero

open Tensor in
/-- Double basis expansion of an element of a tensor product `M ⊗[k] M₂` of two `Tensorial`
  one-index spaces. Given bases `b`, `b2` of `M`, `M₂` coming from the single-index tensor bases,
  every `x : M ⊗[k] M₂` is the double sum over `i, j` of the iterated evaluation coefficient
  `toField (evalT 0 j (evalT 0 i (toTensor x)))` times `b i ⊗ₜ b2 j`. -/
lemma prod_eq_sum_eval {c c2 : C} {M₂ : Type}
    [Tensorial S ![c] M] [AddCommMonoid M₂] [Module k M₂]
    [Tensorial S ![c2] M₂] {b : Module.Basis (basisIdx c) k M}
    {b2 : Module.Basis (basisIdx c2) k M₂}
    (h : b = ((Tensor.basis (S := S) ![c]).map toTensor.symm).reindex ComponentIdx.single)
    (h2 : b2 = ((Tensor.basis (S := S) ![c2]).map toTensor.symm).reindex ComponentIdx.single)
    (x : M ⊗[k] M₂) : x = ∑ i : (basisIdx c), ∑ j : (basisIdx c2),
    toField (evalT 0 (basisIdxCongr (by rfl) j)
      (evalT 0 (basisIdxCongr (by rfl) i) (toTensor x))) • b i ⊗ₜ[k] b2 j := by
  apply toTensor.injective
  conv_lhs => rw [eq_sum_evalT_zero (toTensor x)]
  simp only [map_sum, map_smul]
  refine Finset.sum_congr rfl fun i _ => ?_
  rw [eq_sum_evalT_zero ((evalT 0 i) (toTensor x))]
  simp only [map_sum]
  refine Finset.sum_congr rfl fun j _ => ?_
  simp only [prodT_zero_right, map_smul, permT_basis, prodT_basis']
  congr 1
  subst h h2
  simp only [toTensor_tprod, Function.comp_apply, Module.Basis.coe_reindex, Module.Basis.map_apply,
    LinearEquiv.apply_symm_apply, prodT_basis']
  congr 1
  funext i
  fin_cases i <;> rfl

/-!

## E. Continuous properties

-/

section Continuous

variable {k : Type} [RCLike k] {C : Type} {G : Type} [Group G]
    {V : C → Type} [∀ c, AddCommGroup (V c)] [∀ c, Module k (V c)]
    {basisIdx : C → Type} [∀ c, Fintype (basisIdx c)] [∀ c, DecidableEq (basisIdx c)]
    {rep : (c : C) → Representation k G (V c)} {b : (c : C) → Module.Basis (basisIdx c) k (V c)}
    (S : TensorSpecies k C G V basisIdx rep b) {c : Fin n → C} {M : Type}
    [AddCommGroup M] [Module k M]
    [TopologicalSpace M]

/-!

### E.1. Finite dimensionality

-/
instance [Tensorial S c M] : FiniteDimensional k M := LinearEquiv.finiteDimensional
  (Tensorial.toTensor (M := M)).symm

/-!

### E.2. The map to tensors as a continuous linear equivalence

-/

/-- The map from a type carrying an Tensorial instance to tensors, as a
  continuous linear map. -/
def toTensorCLM [IsTopologicalAddGroup M]
    [ContinuousSMul k M] [Tensorial S c M] [T2Space M] : M ≃L[k] (S.Tensor c) where
  toLinearMap := (Tensorial.toTensor (M := M))
  invFun := (Tensorial.toTensor (M := M)).symm
  left_inv x := by simp
  right_inv x := by simp
  continuous_toFun := by
    let e : M →L[k] (S.Tensor c) := LinearMap.toContinuousLinearMap
      (Tensorial.toTensor (M := M))
    change Continuous e
    exact ContinuousLinearMap.continuous e
  continuous_invFun := by apply IsModuleTopology.continuous_of_linearMap

/-!

### E.3. The Lorentz action as a continuous linear equivalence

-/

/-- The Lorentz action on types carrying a tensorial instance as a continuous linear
  map. -/
noncomputable def actionCLM (g : G) [IsTopologicalAddGroup M]
    [ContinuousSMul k M] [Tensorial S c M] [T2Space M] : M →L[k] M :=
  LinearMap.toContinuousLinearMap (smulLinearMap g)

lemma actionCLM_apply {g : G} [IsTopologicalAddGroup M]
    [ContinuousSMul k M] [Tensorial S c M] [T2Space M] (m : M) :
    actionCLM S g m = g • m := rfl

end Continuous
end Tensorial
